module optics.

import data/nat.
import data/nat-thms.
import data/sigma.
import data/vec.

import functor.
import util.

Lens : ★ ➔ ★ ➔ ★ ➔ ★ ➔ ★
= λ S: ★. λ T: ★. λ A: ★. λ B: ★. ∀ F: ★ ➔ ★. Functor ·F ➔ (A ➔ F ·B) ➔ S ➔ F ·T.

Lens' : ★ ➔ ★ ➔ ★
= λ S: ★. λ A: ★. Lens ·S ·S ·A ·A.

lens : ∀ S: ★. ∀ T: ★. ∀ A: ★. ∀ B: ★. (S ➔ A) ➔ (B ➔ S ➔ T) ➔ Lens ·S ·T ·A ·B
= Λ S. Λ T. Λ A. Λ B. λ get. λ set. Λ F. λ lift. λ f. λ s. lift (flip set s) (f (get s)).

get : ∀ S: ★. ∀ T: ★. ∀ A: ★. ∀ B: ★. Lens ·S ·T ·A ·B ➔ S ➔ A
= Λ S. Λ T. Λ A. Λ B. λ l. l (lift-const ·A) (id ·A).

over : ∀ S: ★. ∀ T: ★. ∀ A: ★. ∀ B: ★. Lens ·S ·T ·A ·B ➔ (A ➔ B) ➔ S ➔ T
= Λ S. Λ T. Λ A. Λ B. λ l. λ f. l lift-id f.

set : ∀ S: ★. ∀ T: ★. ∀ A: ★. ∀ B: ★. Lens ·S ·T ·A ·B ➔ B ➔ S ➔ T
= Λ S. Λ T. Λ A. Λ B. λ l. λ x. over l (const x).

first : ∀ A: ★. ∀ B: ★. ∀ C: ★. Lens ·(Pair ·A ·C) ·(Pair ·B ·C) ·A ·B
= Λ A. Λ B. Λ C. Λ F. λ lift. λ f. λ p. μ' p {
  | pair x y ➔ lift ·B ·(Pair ·B ·C) (λ x. pair x y) (f x)
  }.

second : ∀ A: ★. ∀ B: ★. ∀ C: ★. Lens ·(Pair ·A ·B) ·(Pair ·A ·C) ·B ·C
= Λ A. Λ B. Λ C. Λ F. λ lift. λ f. λ p. μ' p {
  | pair x y ➔ lift ·C ·(Pair ·A ·C) (λ y. pair x y) (f y)
  }.

head : ∀ A: ★. ∀ n: Nat. Lens' ·(Vec ·A (succ n)) ·A
= Λ A. Λ n. Λ F. λ lift. λ f. λ v. μ' v @ λ i: Nat. λ _: Vec ·A i. {i ≃ succ n} ➾ F ·(Vec ·A i) {
  | vnil ➔ Λ p. δ - p
  | vcons -i x xs ➔ Λ _. lift (λ y: A. vcons -i y xs) (f x)
  } -β.

tail : ∀ A: ★. ∀ n: Nat. Lens' ·(Vec ·A (succ n)) ·(Vec ·A n)
= Λ A. Λ n. Λ F. λ lift. λ f. λ v. μ' v @ λ i: Nat. λ _: Vec ·A i. {i ≃ succ n} ➾ F ·(Vec ·A i) {
  | vnil ➔ Λ p. δ - p
  | vcons -i x xs ➔ Λ p. lift (λ ys: Vec ·A i. vcons -i x ys) ((ρ (succInj i n p) - f) xs)
  } -β.
